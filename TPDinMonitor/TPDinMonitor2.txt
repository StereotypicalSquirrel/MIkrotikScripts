#Get voltage date from a Tycon Power TPDIN-Monitor-WEB2 and push it to some random url
:local url "https://remoteserver.com/remoteMonitor.php";

:local progName "TPDinMonitor";
#:log info "$progName: Running...";

:local getBetween do={
	# This is a basic parser, can be used for XML
	# It takes three parameters:
	# inputString - The main string
	# betweenStart - Text AFTER this point will be returned
	# betweenEnd - Text BEFORE this point will be returned
	:local posStart 0;
	:if ([:len $betweenStart] > 0) do={
	:set posStart [:find $inputString $betweenStart]
		:if ([:len $posStart] = 0) do={
			:set posStart 0
		} else={
			:set posStart ($posStart + [:len $betweenStart])
		}
	}

	:local posEnd 9999;
	:if ([:len $betweenEnd] > 0) do={
	:set posEnd [:find $inputString $betweenEnd];
	:if ([:len $posEnd] = 0) do={ :set posEnd 9999 }
	}

	:local result [:pick $inputString $posStart $posEnd];
	:return $result;
}


:if ([:len [/file find name=status.xml]] > 0) do={/file remove status.xml;};

/tool fetch url="http://[TPDinURL]/status.xml" mode=http user=[user] password=[password];
:delay 1; #This is needed to allow time for the file to save
:local statusXML [/file get status.xml contents];
:if ([:len [/file find name=status.xml]] > 0) do={/file remove status.xml};

:if ([:len $statusXML] > 50) do={
	#Build up the URL
	#Web din ver2 do weird results for the relay (0 = off (manual), 1 = on, 2 = off (auto))
	:local relay1 0;
	:local relay2 0;
	:local relay3 0;
	:local relay4 0;
	:if ([$getBetween inputString=$statusXML betweenStart="<ctl0>" betweenEnd="</ctl0>"] = 1) do={ :set relay1 1;};
	:if ([$getBetween inputString=$statusXML betweenStart="<ctl1>" betweenEnd="</ctl1>"] = 1) do={ :set relay2 1;};
	:if ([$getBetween inputString=$statusXML betweenStart="<ctl2>" betweenEnd="</ctl2>"] = 1) do={ :set relay3 1;};
	:if ([$getBetween inputString=$statusXML betweenStart="<ctl3>" betweenEnd="</ctl3>"] = 1) do={ :set relay4 1;};
	:set url ($url . "?led1=" . $relay1);
	:set url ($url . "&led2=" . $relay2);
	:set url ($url . "&led3=" . $relay3);
	:set url ($url . "&led4=" . $relay4);
	:set url ($url . "&amp1=" . [$getBetween inputString=$statusXML betweenStart="<pot0>" betweenEnd="</pot0>"]);
	:set url ($url . "&amp2=" . [$getBetween inputString=$statusXML betweenStart="<pot1>" betweenEnd="</pot1>"]);
	:set url ($url . "&amp3=" . [$getBetween inputString=$statusXML betweenStart="<pot2>" betweenEnd="</pot2>"]);
	:set url ($url . "&amp4=" . [$getBetween inputString=$statusXML betweenStart="<pot3>" betweenEnd="</pot3>"]);
	:set url ($url . "&volt1=" . [$getBetween inputString=$statusXML betweenStart="<pot4>" betweenEnd="</pot4>"]);
	:set url ($url . "&volt2=" . [$getBetween inputString=$statusXML betweenStart="<pot5>" betweenEnd="</pot5>"]);
	:set url ($url . "&volt3=" . [$getBetween inputString=$statusXML betweenStart="<pot6>" betweenEnd="</pot6>"]);
	:set url ($url . "&volt4=" . [$getBetween inputString=$statusXML betweenStart="<pot7>" betweenEnd="</pot7>"]);
	:set url ($url . "&temp1=" . [$getBetween inputString=$statusXML betweenStart="<pot8>" betweenEnd="</pot8>"]);
	:set url ($url . "&temp2=" . [$getBetween inputString=$statusXML betweenStart="<pot9>" betweenEnd="</pot9>"]);
	/tool fetch keep-result=no url=$url
}	
#:log info $url;
